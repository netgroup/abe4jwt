#
# A model for OpenID Connect Authorization Code Flow implemented in AnB 
# specification language. To use this file, download the checker:
# Open-Source Fixed-Point Model-Checker (OFMC)
# from https://www.imm.dtu.dk/~samo/
# (last access on 13th October 2021)
#
# Same model as in 03 Auth_Code_PKCE_noNonce_Flawed.AnB but including a Nonce 
# inside the returned token. PKCE is used as well. Checking this model results
# in a safe flow. 
#

Protocol: OIDC_AuthCodeFlow

Types: Agent    as, #Authorization Server (constant)
	        RS, #Resource Server
	         C; #Client
       Function pw, #shared password between as and C
                pk, #as public key
          resource, #user authorized resource to be accessed
              code, #authorization code
              hash; #PKCE trapdoor function
      Number Scope, #OIDC AuthFlow parameter
             State, #OIDC AuthFlow parameter
             Nonce, #OIDC AuthFlow parameter
              Data, #RS returns this data on successfull authorization
           Session, #shared session between RS and C
          Verifier; #Verifier used in PKCE

Knowledge: 
           as: as,pk(as),inv(pk(as)),C,pw(as,C),code,resource,hash;
           RS: RS,as,pk(as);
            C: C,RS,pk(as),pw(as,C),hash;
         where RS!=C, RS!=as, C!=as

Actions:
C->RS:Scope
RS*->C:Scope,as,Session

[C]*->*as: RS,Scope,State,Nonce,hash(Verifier)
as->[C]: State,code(Scope,State,Nonce),Scope

[C]*->*as:C,pw(as,C),code(Scope,State,Nonce),Verifier 
as*->*[C]:{resource(code(Scope,State,Nonce)),C,as,RS,Nonce}inv(pk(as)),code(Scope,State,Nonce)

[C]*->*RS:{resource(code(Scope,State,Nonce)),C,as,RS,Nonce}inv(pk(as)),Session
RS*->*[C]:Data,Session

Goals:
RS authenticates C on RS,resource(code(Scope,State,Nonce)),C,as,Session
C authenticates RS on Data 
Data secret between RS,C
C authenticates as on State, Scope,code(Scope,State,Nonce)
as weakly authenticates C on C,pw(as,C),code(Scope,State,Nonce)